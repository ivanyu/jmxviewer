/*
 * Copyright 2021 Ivan Yurchenko
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import me.ivanyu.FinalJarTask

plugins {
    id 'java'
    id 'application'
    // https://github.com/johnrengelman/shadow
    id 'com.github.johnrengelman.shadow' version '6.1.0'
}

repositories {
    mavenCentral()
}

dependencies {
    // https://github.com/mabe02/lanterna
    implementation 'com.googlecode.lanterna:lanterna:3.1.1'

    // https://github.com/rhuss/jolokia
    implementation 'org.jolokia:jolokia-core:1.6.2'
}

mainClassName = 'me.ivanyu.jmxviewer.App'

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

jar {
    baseName project.name + '-slim'
    manifest {
        attributes(
                'Class-Path': configurations.compileClasspath.collect { it.getName() }.join(' '),
                'Main-Class': mainClassName
        )
    }
}

shadowJar {
}

task finalJar(type: FinalJarTask) {
    dependsOn shadowJar
    from shadowJar.outputs
}

compileJava {
    options.with {
        compilerArgs.addAll([
                '--add-exports', 'jdk.internal.jvmstat/sun.jvmstat.monitor=ALL-UNNAMED',
                '--add-exports', 'jdk.attach/sun.tools.attach=ALL-UNNAMED'
        ])
    }
}

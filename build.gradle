plugins {
    id 'java'
    id 'application'
    // https://github.com/johnrengelman/shadow
    id 'com.github.johnrengelman.shadow' version '6.1.0'
}

repositories {
    mavenCentral()
}

dependencies {
    // https://github.com/caoccao/Javet/
    implementation 'com.caoccao.javet:javet:0.8.3'

    // https://github.com/rhuss/jolokia
    implementation 'org.jolokia:jolokia-core:1.6.2'
}

mainClassName = 'me.ivanyu.jmxviewer.App'

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

sourceSets {
    main {
        resources {
            srcDirs('js/')
            include('bundle.js')
        }
    }
}

jar {
    manifest {
        attributes(
                'Class-Path': configurations.compileClasspath.collect { it.getName() }.join(' '),
                'Main-Class': mainClassName
        )
    }
}

shadowJar {
    // Remove the Windows DLLs and V8 version.
    exclude 'libjavet-*.dll'
    exclude 'libjavet-v8-*.so'
}

compileJava {
    options.with {
        compilerArgs.addAll([
                '--add-exports', 'jdk.internal.jvmstat/sun.jvmstat.monitor=ALL-UNNAMED',
                '--add-exports', 'jdk.attach/sun.tools.attach=ALL-UNNAMED'
        ])
    }
}

/*
 * Copyright 2021 Ivan Yurchenko
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import me.ivanyu.FinalJarTask

plugins {
    id 'java'
    id 'application'
    // https://github.com/johnrengelman/shadow
    id 'com.github.johnrengelman.shadow' version '7.0.0'
}

repositories {
    mavenCentral()
}

dependencies {
    // https://github.com/mabe02/lanterna
    implementation 'com.googlecode.lanterna:lanterna:3.1.1'

    // https://github.com/rhuss/jolokia
    implementation 'org.jolokia:jolokia-core:1.6.2'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.2'
    testImplementation 'org.mockito:mockito-core:3.11.2'
}

mainClassName = 'me.ivanyu.jmxviewer.App'

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

processResources {
    filesMatching('version.properties') {
        expand(version: version)
    }
}

jar {
    setArchiveBaseName(project.name + '-slim')
    manifest {
        attributes(
                'Class-Path': configurations.compileClasspath.collect { it.getName() }.join(' '),
                'Main-Class': mainClassName,
                'Version': "${project.version}"
        )
    }
}

shadowJar {
    minimize {
        // Blind exclusion of Jolokia doesn't work, because some classes are referenced dynamically
        // in the run time. We do point exclusions in FinalJarTask.
        exclude(dependency('org.jolokia:.*:.*'))
    }
}

task finalJar(type: FinalJarTask) {
    dependsOn shadowJar
    from shadowJar.outputs
    outputs.file("build/jmxviewer.jar")
}

compileJava {
    options.with {
        compilerArgs.addAll([
                '--add-exports', 'jdk.internal.jvmstat/sun.jvmstat.monitor=ALL-UNNAMED',
                '--add-exports', 'jdk.attach/sun.tools.attach=ALL-UNNAMED'
        ])
    }
}

test {
    useJUnitPlatform()
}
